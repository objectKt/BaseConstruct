plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.jetbrainsKotlinAndroid)
}

def ver = libs.versions

android {
    namespace ver.appId.get().toString()
    ndkVersion '25.1.8937393'
    compileSdk ver.compileVersion.get().toInteger()

    defaultConfig {
        applicationId ver.appId.get().toString()
        minSdk ver.minSdkVersion.get().toInteger()
        targetSdk ver.compileVersion.get().toInteger()
        versionCode ver.versionCode.get().toInteger()
        versionName ver.versionName.get().toString()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def listSubFile = {
        def resFolder = 'src/main/res/layouts'
        def files = file(resFolder).listFiles()
        def folders = []
        files.each { item -> folders.add(item.absolutePath) }
        folders.add(file(resFolder).parentFile.absolutePath)
        return folders
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions { jvmTarget = '1.8' }
    sourceSets { main { res.srcDirs = listSubFile() } }

    // Flavor Dimension 配置不同的多个 app 的特性
}

dependencies {
    implementation project(":libraryAuto")
    implementation libs.androidx.activity
    implementation libs.androidx.constraintlayout
    implementation libs.github.net
    implementation libs.squareup.okhttp
    implementation libs.androidx.appcompat
    implementation libs.androidx.material
}