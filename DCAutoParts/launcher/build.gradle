plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.jetbrainsKotlinAndroid)
}
apply plugin: "kotlin-kapt"

def ver = libs.versions

android {
    namespace ver.appId.get().toString()
    ndkVersion '25.1.8937393'
    compileSdk ver.compileVersion.get().toInteger()

    defaultConfig {
        applicationId ver.appId.get().toString()
        minSdk ver.minSdkVersion.get().toInteger()
        targetSdk ver.compileVersion.get().toInteger()
        versionCode ver.versionCode.get().toInteger()
        versionName ver.versionName.get().toString()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def listSubFile = {
        def resFolder = 'src/main/res/layouts'
        def files = file(resFolder).listFiles()
        def folders = []
        files.each { item -> folders.add(item.absolutePath) }
        folders.add(file(resFolder).parentFile.absolutePath)
        return folders
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions { jvmTarget = '1.8' }
    buildFeatures.dataBinding = true
    sourceSets { main { res.srcDirs = listSubFile() } }

    // Flavor Dimension 配置不同的多个 app 的特性
}

dependencies {
    implementation project(":lib-auto")
    implementation libs.androidx.activity
    implementation libs.androidx.appcompat
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.material
    implementation libs.github.usb
    implementation libs.squareup.okhttp
    implementation libs.androidx.legacy.support.v4
    implementation libs.liangjingkanji.net
    implementation libs.liangjingkanji.list
    implementation libs.liangjingkanji.event
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.navigation.fragment
}